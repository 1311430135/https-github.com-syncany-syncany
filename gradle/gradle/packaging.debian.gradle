// Packaging ///////////////////////////////////////////////////////////////////

task debianClean(type: Delete) {
	delete 'build/debian'	
}

task debianPrepare(dependsOn: [installApp, debianClean]) {
	String debianApplicationVersionFull = "${applicationVersionFull}".replaceAll("-", "~")
	String applicationSigningAuthor = "Philipp Heckel <philipp.heckel@gmail.com>"
	
	doLast {
		copy {
			from rootProject.files("build/install/syncany") 
			into rootProject.file("build/debian/syncany")
			exclude("**/*.bat")
		}
	
		copy {
			from rootProject.files("build/install/syncany/LICENSE.md") 
			into rootProject.file("build/debian/syncany/debian")
			rename("LICENSE.md", "copyright")
		}

		copy {
			from rootProject.files("gradle/debian/debian") 
			into rootProject.file("build/debian/syncany/debian")
		}
	
		copy {
			from rootProject.files("gradle/debian/debian/changelog") 
			expand([
				applicationVersion: "${debianApplicationVersionFull}",
				applicationSigningAuthor: "${applicationSigningAuthor}",
				applicationDate: "${applicationDate}"
			])
			into rootProject.file("build/debian/syncany/debian")
		}
	}
}

task debian(dependsOn: debianPrepare) {
	doLast {
		exec {
			workingDir rootProject.file("build/debian/syncany")
			commandLine "debuild"
		}
	}
}

task debianPPA(dependsOn: debianPrepare) {
	doLast {
		exec {
			workingDir rootProject.file("build/debian/syncany")
			commandLine "debuild -S".split()
		}
	}
}
